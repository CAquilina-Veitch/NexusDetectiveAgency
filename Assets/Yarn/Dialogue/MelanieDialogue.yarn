title: MelanieDialogue
tags:
---

<<declare $animNumber = 0>>
<<declare $documentsFound = 0>>

DEBUG: How many documents has the player found?
->	Only a few.
		<<set $documentsFound += 2>>
->	Enough to proceed.
		<<set $documentsFound += 5>>

<<wait 1>>
<<jump TalkingToMelanie1>>

===
title: TalkingToMelanie1
---

Melanie: Who are you?
Melanie: How on Earth did you get here?

->	Could say the same.
		Max: Could say the same thing about you.
		Melanie: Well I work here, I'm allowed to be here.
->	I'm Detective Max.
		Max: My name is Max, I'm a detective.
		Melanie: Well Max, you know you are technically trespassing, right?

Melanie: You should be leaving. Now. Before I call security.

->	I can't do that.
		Max: Not so fast.
		Max: I need to see the device you've been working on.
		<<jump TalkingToMelanie2>>
->	Fine (Leave).
		Max: Well alright then.
		<<stop>>

===
title: TalkingToMelanie2
---

Melanie: What device? We don't have anything down here.
Melanie: We haven't been working on anything for years.
Melanie: Everything we had been working on is in our main displays in the lobby.

->	There's something here.
		Max: I know there is something else here.
		Max: And I know it's affecting this world.

Melanie: Well there simply isn't.

<<if $documentsFound < 5 >>
		<<jump TalkingToMelanieFail>>
<<else>>
		<<jump TalkingToMelaniePass>>
<<endif>>
===
title: TalkingToMelanieFail
---

->	I don't believe you.
		Max: Well, I simply don't believe that.

Melanie: Fine.
Melanie: If you desperately want to see what's behind here, get this file.
Melanie: It's going to catch you up to speed a little (Leave).
<<stop>>

===
title: TalkingToMelaniePass
---
->	Stop bluffing.
		Max: Stop bluffing.
		Max: I know there is something behind that door. The CPU broke.

Melanie: Wait... What?
Melanie: How... could you possibly know that?

//Max mumbles: I... Know... All...
Max: I know lots of things, like how you authorized the improper use of the machine.
Max: How Bridger R&D wanted to abuse this device to make a profit of innocents.
Max: Like how your guilt and shame mean more than your job.

Melanie: ...

Max: I can help you fix this mess.

Melanie: You are certainly good at your job.
Melanie: But fixing this machine is impossible.

//Max mumbles: Impossible?
Max: What do you mean by "Fixing it is impossible"?

Melanie: Well. Ummm.
Melanie: We, ehh... lost... the schematics.
Melanie: A poor filing choice from an intern.

//Max mumbles: Who... knows
Max: Does no one know that it broke?

Melanie: We hid it, out of shame.
Melanie: No one knows besides the team that worked on it.
Melanie: We had to think about what would happen if someone did find out about it.
Melanie: It also has other uses, some less... economical, and dangerous in the wrong hands.

//Max mumbles: Pure... Incompetence
Max: More gross incompetence.
Max: The sad thing is, the machine would have worked.
Max: In a separate dimension, the machine worked to clean the air.
Max: Greenery populates the world.

Melanie: Jesus ... .You can travel dimensions?
<<wait  1>>
Melanie: I'm getting off track.
Melanie: I suppose it's comforting to know the machine was used for its correct purpose. That is what we designed the machine for.
Melanie: We are responsible for bringing doom upon ourselves by modifying the machine.
Melanie: We made the wrong choice and paid dearly for it.

<<jump TalkingToMelanieEnd>>

===