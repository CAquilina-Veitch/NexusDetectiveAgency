title: MelanieDialogue
tags:
position: -1013,-832
---
<<declare $animNumber = 0>>
<<declare $audioMelanieNumber = 0>>
<<declare $audioMaxNumber = 0>>
<<declare $documentsFound = 0>>
<<declare $firstInteraction = true>>
<<declare $FetchDocFound = false>>
<<declare $MealnieActive = true>>

DEBUG: How many documents has the player found?
->	Only a few.
		<<set $documentsFound to 1>>
->	Enough to proceed.
		<<set $documentsFound to 5>>
<<wait 1>>
<<jump MelanieInteract>>
===


title: MelanieInteract
position: -258,-837
---
<<if $firstInteraction == true>>
	<<jump TalkingToMelanie1>>
<<else>>
	<<jump MelanieDocLoop>>
<<endif>>
===


title: TalkingToMelanie1
position: -822,-561
---
<<set $firstInteraction = false>>

Melanie: Who are you?
Melanie: How did you get in here? #melanie01

->	Could say the same.
		Max: Could say the same thing about you.
		Melanie: Well I work here, I'm allowed to be here. #melanie02
->	I'm Detective Max.
		Max: My name is Max, I'm a detective.
		Melanie: Well Max, you know you are trespassing, right? #melanie03
<<jump TalkingToMelanie2>>
===


title: TalkingToMelanie2
position:
position: -822,-286
---
Melanie: You'll be leaving now. Before I call security. #melanie04

->	I can't do that.
		Max: Not so fast.
		Max: I need to see the device you've been working on.
		<<jump TalkingToMelanie3>>
->	Fine (Leave).
		Max: Well alright then.
		<<wait  1>>
		<<jump MelanieExit>>
===


title: TalkingToMelanie3
position: -823,-92
---
Melanie: What device? We don't have anything down here. We haven't been working on anything for years. Everything we had been working on is in our main displays in the lobby. #melanie05

->	There's something here.
		Max: I know there is something else here.
		Max: And I know it's affecting this world.

Melanie: Well there simply isn't. #melanie06
<<if $documentsFound < 5 >>
		<<jump MelanieFail>>
<<else>>
		<<jump MelaniePass>>
<<endif>>
===

title: MelanieDocLoop
position:
position: -258,-561
---
<<if $FetchDocFound == false>>
	<<jump TalkingToMelanie2>>
<<else>>
	<<jump MelanieRepeat>>
<<endif>>
===


title: MelanieFail
position: -541,-286
---
->	I don't believe you.
		Max: Well, I simply don't believe that.

Melanie: Fine. If you desperately want to see what's behind here, get this file. It's going to catch you up to speed a little (Leave). #melanie07
<<set $FetchDocFound = false>>
<<jump ExitingForQuest>>
===


title: ExitingForQuest
position:
position: -262,-286
---
DEBUG: Max leaves Melanie in search for her documents.
<<wait  1>>
DEBUG: Did Max find the required documents?
->	Yes.
		<<set $FetchDocFound = true>>
		<<jump MelanieExit>>
->	No.
		<<jump MelanieExit>>

===


title: MelanieRepeat
position:
position: 265,-442
---

//Max mumbles: Found... it...
Max: I found the file you were looking for.

Melanie: About time. Any questions?

->	Max: What about a new part?
		//Max mumbles: "No... New... part?"
		Max: So I can't create a new part?

		Melanie: There was only a single unit ever created, due to the fact this project was purely experimental.
		Melanie:Because of the lost schematics, the only way to make the machine work was if there was another part somewhere.

->	Max: But a new part will fix this?
		//Max mumbles: "Can... I... Fix... This..."
		Max: So you're saying that if I either find a way to fix the machine, or get a new part, the machine will be functional again?

		Melanie: Yes, that would be the case.
		Melanie: By getting a new part, we can repair it to its full capacity.
		Melanie: Hopefully, we could fix all the shame we brought upon ourselves.

<<jump MelanieFinal>>
===


title: MelaniePass
position: -256,-93
---
->	Stop bluffing.
		Max: Stop bluffing.
		Max: I know there is something behind that door. The CPU broke.

		Melanie: Wait... What? How... could you possibly know that? #melanie08

->	Max: I know a lot more.
		//Max mumbles: I... Know... All...
		Max: I know lots of things, like how you authorized the improper use of the machine.
		Max: How Bridger R&D wanted to abuse this device to make a profit of innocents.
		Max: Like how your guilt and shame mean more than your job.

		Melanie: ...

Max: I can help you fix this mess.

Melanie: You certainly are good at your job. But fixing this machine is impossible. #melanie09
<<jump MelaniePassTree>>
===


title: MelaniePassTree
position:
position: 7,-94
---

->	Max: What do you mean by "Fixing it is impossible"?
		//Max mumbles: Impossible?
		Melanie: Well. Ummm. We, ehh... lost... the schematics. A poor filing choice from an intern. #melanie10

->	Max: Does no one know that it broke?
		//Max mumbles: Who... knows
		Melanie: We hid it, out of shame.
		Melanie: No one knows besides the team that worked on it.
		Melanie: We had to think about what would happen if someone did find out about it.
		Melanie: It also has other uses, some less... economical, and dangerous in the wrong hands. #melanie11

->	Max: More gross incompetence.
		//Max mumbles: Pure... Incompetence
		Max: The sad thing is, the machine would have worked.
		Max: In a separate dimension, the machine worked to clean the air.
		Max: Greenery populates the world.

		Melanie: Jesus ... .You can travel dimensions?
		<<wait  1>>
		Melanie: I'm getting off track.
		Melanie: I suppose it's comforting to know the machine was used for its correct purpose. That is what we designed the machine for.
		Melanie: We are responsible for bringing doom upon ourselves by modifying the machine.
		Melanie: We made the wrong choice and paid dearly for it.

<<wait 1>>
<<jump MelanieFinal>>
===


title: MelanieFinal
position: 264,-94
---
//Max mumbles: I... Think... I... can... fix... it)
Max: I may be able to fix it.
Max: I can try and find a part for you from the other dimension.

Melanie: Really?
Melanie: If you fixed this, you could save countless lives.
<<wait  1>>
Melanie: Oh, and one more thing.
Melanie: Good luck.
<<jump MelanieExit>>
===

title: MelanieExit
position:
---
<<set $MealnieActive = false>>
<<stop>>
===